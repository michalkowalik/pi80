.program databus
; wait for interrupt
; read 9 bits
; bit 0: 0 is read, 1 is write
; bits 1 to 8 -> data to write

.wrap_target
  pull
  out x, 1               ; shift one bit to the X scratch register
  jmp x-- write          ; if LSB = 1 => set pins

read:
  in pins, 8             ; read 8 pins
  nop [1]                ; wait for the input to settle
  push                   ; push to input register
  jmp reset_bus          ; reset bus

write:
  mov x, osr             ; fill X scratch register with OSR
  mov osr, !null         ; fill OSR with ones
  out pindirs 8          ; set DATABUS as outputs
  nop [1]                ; wait for the output to settlea

  mov osr, x             ; fill OSR with X scratch register
  out pins, 8            ; shift last 8 bits from OSR to the databus bins
                         ; and wait one cycle for the output to settle
  nop [31]

reset_bus:
  mov x, null            ; fill X scratch register with zeros
  mov osr, null          ; fill OSR with zeros
  out pindirs, 8         ; change pin direction to input
.wrap


% c-sdk {
    static inline void databus_program_init(PIO pio, uint sm, uint offset, uint pin, uint pin_count) {
      pio_sm_config c = databus_program_get_default_config(offset);

      sm_config_set_out_pins(&c, pin, pin_count);
      sm_config_set_in_pins(&c, pin);

      for(uint i = 0; i < pin_count; i++) {
        pio_gpio_init(pio, pin + i);
      }

      // debug: slow down the clock
      sm_config_set_clkdiv(&c, 40000.0f);

      // set PINS to input
      pio_sm_set_consecutive_pindirs(pio, sm, pin, pin_count, false);

      pio_sm_init(pio, sm, offset, &c);
    }
%}